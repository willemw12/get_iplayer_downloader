# Maintainer: willemw <willemw12@gmail.com>
# Contributor: Mark Foxwell <fastfret79@archlinux.org.uk>
# Contributor: AshtonBRSC <michael@ashtonbrsc.co.uk>
# Contributor: pluckypigeon <pluckypigeon@ArchForums.com>
# Contributor: canton7

#PATCH
#pkgname=get_iplayer-git
pkgname=get_iplayer-patched-git

pkgver=2.91
pkgrel=2
pkgdesc="Download TV and radio from BBC iPlayer"
arch=('any')
url="https://github.com/get-iplayer/get_iplayer"
license=('GPL3')
#  'perl-json: metadata parsing'
#  'perl-xml-simple: metadata parsing'
depends=('perl-html-parser' 'perl-http-cookies' 'perl-libwww' 'perl-net-http' 'perl-www-mechanize')
#  'perl-net-smtps: Mail search results via TLS (alternative)'
optdepends=(
  #'atomicparsley: MP4 and M4A files metadata tagging. Newer "atomicparsley-largefile-hg" is preferred'
  #
  # Having only 'optdepends atomicparsley' is sufficient to install either atomicparsley or atomicparsley-largefile-hg.
  # However, added 'optdepends atomicparsley-largefile-hg' so 'pacman -Qi' will show 'atomicparsley-largefile-hg [installed]'
  'atomicparsley: MP4 and M4A files metadata tagging'
  'atomicparsley-largefile-hg: MP4 and M4A files metadata tagging (preferred)'

  'ffmpeg: FLV files to MP4, M4A, MP3 or AVI files'
  'flvstreamer: Flash audio/video streams to FLV files. An "rtmpdump" alternative'
  'id3v2: MP3 files metadata tagging. Alternative "perl-mp3-tag" is preferred (full ID3 tagging)'
  'lame: MP3 encoder'
  'mplayer: FLV files to MP3 files. Windows Media audio streams to WMA files'
  'perl-authen-sasl: Mail search results via SSL'
  'perl-mp3-info: Catalogue MP3 files in localfiles plugin'
  'perl-mp3-tag: MP3 files metadata tagging'
  'perl-net-smtp-ssl: Mail search results via SSL'
  'perl-net-smtp-tls: Mail search results via TLS'
  'perl-xml-simple: metadata parsing'
  'rtmpdump: Flash audio/video streams to FLV files'
  'vlc: 3gp / N95 h.264 streams')
makedepends=('git')
conflicts=('get_iplayer')
provides=('get_iplayer')
#source=($pkgname::git://git.infradead.org/get_iplayer.git)
source=($pkgname::git://github.com/get-iplayer/get_iplayer.git)
md5sums=(SKIP)

pkgver() {
  cd $pkgname
  #printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  git describe --match "v*" --tags | sed -r 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

#PATCH
build() {
  cd $pkgname

  # Apply the following patches
  #
  ##my $info_limit      = 40;
  #my $info_limit      = 200;
  #
  ##$mp3->select_id3v2_frame_by_descr('COMM(eng,#0)[]', $tags->{comment});
  #$mp3->select_id3v2_frame_by_descr('COMM(eng,#0)[]', $tags->{longDescription});
  #
  ##'--COMM', $tags->{comment},
  #'--COMM', $tags->{longDescription},
  #
  ##if ( $cattribs->{kind} eq 'akamai' ) {
  #if ( $cattribs->{kind} eq 'DISABLED_akamai' ) {

  # Patch: increase download list size
  sed -i "s|^my \$info_limit.*|\n#PATCH\n#&\nmy \$info_limit      = 200;\n|" get_iplayer

  # Patch: store full description in id3v2 comment tag. Preserve indentation in the patch
  sed -i "s|\(^[ \t]*\)\(\$mp3->select_id3v2_frame_by_descr('COMM(eng,#0)\[\]', \$tags->{comment});.*\)|\n\1#PATCH\n\1#\2\n\1\$mp3->select_id3v2_frame_by_descr('COMM(eng,#0)\[\]', \$tags->{longDescription});\n|" get_iplayer
  sed -i "s|\(^[ \t]*\)\('--COMM', \$tags->{comment},.*\)|\n\1#PATCH\n\1#\2\n\1'--COMM', \$tags->{longDescription},\n|" get_iplayer

  # Patch: disable Akamai
  sed -i "s|\(^[ \t]*\)\(if ( \$cattribs->{kind} eq 'akamai' ) {.*\)|\n\1#PATCH\n\1#\2\n\1if ( \$cattribs->{kind} eq 'DISABLED_akamai' ) {\n|" get_iplayer

  # Radio categories in search results
  #NOTE Or use --refresh-feeds=ion (listview feed) or --refresh-feeds=ion2 (atoz feed)
  sed -i "s|\(^[ \t]*\)\(.*\)\(\$self->get_links_aod\)\(.*\)|\1#PATCH\n\1#\2\3\4\n\1\2\$self->get_links_ion\4|" get_iplayer

  # Check applied patches
  if [ $(grep "#PATCH" get_iplayer | wc -l) -ne 5 ]; then
    echo "  -> ERROR: incorrect number of patched lines in /usr/bin/get_iplayer" >&2
  fi
}

package() {
  cd $pkgname
  install -Dm755 get_iplayer "$pkgdir/usr/bin/get_iplayer"
  install -Dm755 get_iplayer.cgi "$pkgdir/usr/bin/get_iplayer.cgi"

  install -Dm644 README.md "$pkgdir/usr/share/doc/get_iplayer/README.md"

  install -Dm644 get_iplayer.1 "$pkgdir/usr/share/man/man1/get_iplayer.1"

  install -dm755 "$pkgdir/usr/share/get_iplayer/plugins"
  install -m644 plugins/*.plugin plugins/plugin.template "$pkgdir/usr/share/get_iplayer/plugins/"
}

